entrypoints Exp;

-- Metavariables
MVar. Var ::= Ident;                        -- variables
MUni. Uni ::= Ident;                        -- unitaries

-- Types
TQref. Typ ::= "qref";                      -- qubit reference
TParr. Typ ::= Typ "->" Typ;                -- partial function
TCmd.  Typ ::= "cmd" "(" Typ ")";           -- command
TProd. Typ ::= Typ "*" Typ;
TBool. Typ ::= "bool";
TUnit. Typ ::= "unit";

-- Expressions
EVar.  Exp ::= Var;
ELet.  Exp ::= "let" Var "be" Exp "in" Exp;   -- let expression
ELam.  Exp ::= "\\" "(" Var ":" Typ ")" Exp;  -- lambda abstraction
EAp.   Exp ::= Exp "(" Exp ")";               -- application
ECmd.  Exp ::= "cmd" Cmd;                     -- encapsulation
ETpl.  Exp ::= "(" Exp "," Exp ")";
EProj. Exp ::= Exp "." Integer;               -- currently just 1 or 2 as these are pairs
ETrue. Exp ::= "true";
EFls.  Exp ::= "false";
EIte.  Exp ::= "if" Exp "then" Exp "else" Exp;
ENot.  Exp ::= "not" Exp;
ETriv. Exp ::= "()";
ERef.  Exp ::= Exp "requires" Ref "ensures" Ref;

-- Commands
CRet.   Cmd ::= "ret" Exp;                                -- return
CBnd.   Cmd ::= "bnd" Var "<-" Exp ";" Cmd;               -- sequence
CNew.   Cmd ::= "new" Var "in" Cmd;                       -- new qubit reference
CGap.   Cmd ::= Uni "(" Exp ")";                          -- unitary gate application
CDiag.  Cmd ::= "D" "(" Uni "," Uni ";" Exp "," Exp ")";  -- diagonal gate application
CMeas.  Cmd ::= "meas" "(" Exp ")";                       -- measurement

-- Refinements
RTop. Ref ::= "top";
RBot. Ref ::= "bot";
REq.  Ref ::= Var "=" Integer;
RAnd. Ref ::= Ref "/\\" Ref;
ROr.  Ref ::= Ref "\\/" Ref;
RUni. Ref ::= Uni "o" Ref;      -- unitary applied to a subspace

-- Programming idioms

-- sequential composition of commands
seq. Cmd ::= "{" Var "<-" Cmd ";" Cmd "}";
define seq x m1 m2 = CBnd x (ECmd m1) m2;

-- `do e` executes an encapsulated command and returns its result
-- This encoding requires an explicit variable argument
-- Example: do 1 x
do. Exp ::= "do" Exp Var;
define do e x = ECmd (CBnd x e (CRet (EVar x)));

-- procedure
proc. Exp ::= "proc" "(" Var ":" Typ ")" Cmd;
define proc x t m = ELam x t (ECmd m);

-- procedure call
-- This encoding also requires an explicit variable argument
pcall. Exp ::= "call" Exp "(" Exp ")" Var;
define pcall e1 e2 x = ECmd (CBnd x (EAp e1 e2) (CRet (EVar x)));;

internal EVarT. Exp ::= "(" Var ":" Typ ")";

{-
# Examples

## QAlgol Example 3.1

cmd new q1 in
ret let q2 be q1 in
cmd D(I, X; q1, q2)

## QAlgol Example 3.2

let newQbit be proc(dummy:unit)
  new x in ret x
in ()

## WIP example with specification

bell00((a,b))
requires a=0 /\ b=0
ensures c=0

-}
